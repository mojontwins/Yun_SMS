#include "cmdlineparser.bi"
#include "mtparser.bi"

Sub usage
	Print "Usage:"
	Print 
	Print "$ generatebank.exe scan=directory out=fileprefix bank=n"
	Print
	Print "Will scan directory and build fileprefix.h and fileprefix.c, bank should carry"
	Print "the destination bank number."
End Sub

Dim As String mandatory (2) = { "scan", "out", "bank" }
Dim As String mainPath, inputLine, tokens (255), outputLine, m, fileName
Dim As Integer fIn, fOutH, fOutC, index, i, narrs

Print "generatebank v0.1 "

sclpParseAttrs

If Not sclpCheck (mandatory ()) Then usage: End

fOutH = FreeFile
Kill sclpGetValue ("out") & ".h"
Open sclpGetValue ("out") & ".h" For Output As fOutH

Print #fOutH, "// " & sclpGetValue ("out") & ".h generated by generateBank v0.1"
Print #fOutH, "// Copyleft 2016 the Mojon Twins"
Print #fOutH, ""

fOutC = FreeFile
Kill sclpGetValue ("out") & ".c"
Open sclpGetValue ("out") & ".c" For Output As fOutC

Print #fOutC, "// " & sclpGetValue ("out") & ".c generated by generateBank v0.1"
Print #fOutC, "// Copyleft 2016 the Mojon Twins"
Print #fOutC, ""

mainPath = sclpGetValue ("scan")

' Iterate directory'
fileName = Dir (mainPath & "\*", &H21)
While Len (fileName)
	fIn = FreeFile: Open mainPath & "\" & fileName For Input As #fIn
	Print "Reading " & fileName & "... ";
	narrs = 0
	Print #fOutC, "// File: " & fileName
	Print #fOutC, ""
	
	While Not Eof (fIn) 
		Line Input #fIn, inputLine
		parseTokenizeString inputLine, tokens (), "[]={};,*" + Chr (9), "/"

		' First, write to the .c file as is
		If tokens (0) <> "#define" Then	Print #fOutC, inputLine

		' Now detect.
		inputLine = Trim (inputLine, Any " " + Chr (9))
		
		' const
		If tokens (0) = "const" Then
			' Generate a bank define, find first word not a C identifier
			index = 1
			While tokens (index) = "unsigned" Or tokens (index) = "signed" _
				Or tokens (index) = "char" Or tokens (index) = "int"
				index = index + 1
			Wend

			Print #fOutH, "#define " & ucase (tokens (index)) & "_BANK " & sclpGetValue ("bank")

			' Copy definition:
			outputLine = "extern "
			For i = 1 To Len (inputLine)
				m = Mid (inputLine, i, 1)
				If m = "=" Then Exit For
				If m = Chr (9) Then m = " "
				outputLine = outputLine & m 
			Next i	
			outputLine = Trim (outputLine) & ";"

			Print #fOutH, outputLine
			Print #fOutH, ""
			narrs = narrs + 1
		ElseIf tokens (0) = "#define" Then
			Mid (inputLine, Instr (inputLine, tokens (1)), Len (tokens (1))) = Ucase (tokens (1))
			Print #fOutH, inputLine
		End If
	Wend
	Close #fIn
	Print #fOutC, "// File: " & fileName & " - EOF"
	Print #fOutC, ""

	Print "" & narrs & " array";
	If narrs <> 1 Then Print "s";
	Print " found."

	' Next
	fileName = Dir ()
Wend

Close fOutH, fOutC
? "DONE"
